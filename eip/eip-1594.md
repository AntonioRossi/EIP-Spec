---

eip: ERC-1594
title: Security Token Standard
author: Adam Dossa (@adamdossa), Pablo Ruiz (@pabloruiz55), Fabian Vogelsteller (@frozeman), Stephane Gosselin (@thegostep)
discussions-to: #1411
status: Draft
type: Standards Track
category: ERC
created: 2018-09-09
require: ERC-20 (#20), ERC-1066 (#1066)

---

## Simple Summary

A standard interface for issuing security tokens, managing their ownership and transfer restrictions.

This standard represents a decomposition of ERC-1400 (#1411) to split out the partition functionality (ERC-1410 #1410) from the remainder of the security token functionality.

It further updates ERC-1400 (#1411) to:  
  - use ERC-20 rather than ERC-777 as a base
  - split out partitions (ERC-1410) from the security token standard
  - add clearer issuance and redemption mechanics
  - add an event for document management to allow communication with token holders based off event subscriptions
  - extend ERC-20 `transfer` functions to `transferWithData` to allow off-chain data to be injected into token transfers

## Abstract

Incorporates document management, error signalling, gate keeper (operator) access control, off-chain data injection and issuance / redemption semantics.

This standard inherits from ERC-20 (#20) and could be easily extended to meet the ERC-777 (#777) standard if needed.

## Motivation

Accelerate the issuance and management of securities on the Ethereum blockchain by specifying a standard interface through which security tokens can be operated on and interrogated by all relevant parties.

Security tokens differ materially from other token use-cases, with more complex interactions between off-chain and on-chain actors, and considerable regulatory scrutiny.

Security tokens should be able to represent any asset class, be issued and managed across any jurisdiction, and comply with the associated regulatory restrictions.

## Requirements

Moving the issuance, trading and lifecycle events of a security onto a public ledger requires having a standard way of modelling securities, their ownership and their properties on-chain.

The following requirements have been compiled following discussions with parties across the Security Token ecosystem.

- MUST have a standard interface to query if a transfer would be successful and return a reason for failure.
- MUST be able to perform forced transfer for legal action or fund recovery.
- MUST emit standard events for issuance and redemption.
- MAY require signed data to be passed into a transfer transaction in order to validate it on-chain.
- SHOULD NOT restrict the range of asset classes across jurisdictions which can be represented.
- MUST be ERC-20 (#20) compatible.
- COULD be ERC-777 (#777) compatible.

## Rationale

### Document Management

Being able to attach documents to a security token allows the issuer, or other authorised entities, to communicate documentation associated with the security to token holders. An attached document can optionally include a hash of its contents in order to provide an immutability guarantee.

### Transfer Restrictions

Transfers of securities can fail for a variety of reasons in contrast to utility tokens which generally only require the sender to have a sufficient balance.

These conditions could be related to metadata of the securities being transferred (i.e. whether they are subject to a lock-up period), the identity of the sender and receiver of the securities (i.e. whether they have been through a KYC process, whether they are accredited or an affiliate of the issuer) or for reasons unrelated to the specific transfer but instead set at the token level (i.e. the token contract enforces a maximum number of investors or a cap on the percentage held by any single investor).

For ERC-20 / ERC-777 tokens, the `balanceOf` and `allowance` functions provide a way to check that a transfer is likely to succeed before executing the transfer, which can be executed both on and off-chain.

For tokens representing securities the standard introduces a function `canTransfer` which provides a more general purpose way to achieve this when the reasons for failure are more complex; and a function of the whole transfer (i.e. includes any data sent with the transfer and the receiver of the securities).

In order to support off-chain data inputs to transfer functions, transfer functions are extended to `transferWithData` / `transferFromWithData` which can optionally take an additional `bytes _data` parameter.

In order to provide a richer result than just true or false, a byte return code is returned. This allows us to give a reason for why the transfer failed, or at least which category of reason the failure was in. The ability to query documents and the expected success of a transfer is included in Security Token section.

### Transparent Control

A token representing ownership in a security may require authorised operators to have additional controls over the tokens.

This includes the ability to issue additional supply, as well as make forced transfers of tokens. The standard allows these controls to be managed and also critically ensures their transparency. If an issuer requires the ability to issue additional tokens, or make controller transfers (forced transfers) then these rights can be transparently assessed rather than being implemented in a bespoke or obfuscated manner.

## Specification

### Document Management

#### getDocument / setDocument

These functions are used to manage a library of documents associated with the token. These documents can be legal documents, or other reference materials.

A document is associated with a short name (represented as a `bytes32`) and can optionally have a hash of the document contents associated with it on-chain.

It is referenced via a generic URI that could point to a website or other document portal.

`setDocument` MUST emit a `Document` event with details of the document being attached or modified.

``` solidity
function getDocument(bytes32 _name) external view returns (string, bytes32);
function setDocument(bytes32 _name, string _uri, bytes32 _documentHash) external;
```

### Restricted Transfers

#### canTransfer

Transfers of securities may fail for a number of reasons, for example relating to:
  - the identity of the sender or receiver of the tokens
  - limits placed on the specific tokens being transferred (i.e. lockups on certain quantities of token)
  - limits related to the overall state of the token (i.e. total number of investors)

The standard provides an on-chain function to determine whether a transfer will succeed, and return details indicating the reason if the transfer is not valid.

These rules can either be defined using smart contracts and on-chain data, or rely on `_data` passed as part of the `transferWithData` function which could represent authorisation for the transfer (e.g. a signed message by a transfer agent attesting to the validity of this specific transfer).

The function will return both a ESC (Ethereum Status Code) following the EIP-1066 standard, and an additional `bytes32` parameter that can be used to define application specific reason codes with additional details (for example the transfer restriction rule responsible for making the transfer operation invalid).

If `bytes _data` is empty, then this corresponds to a check on whether a `transfer` (or `transferFrom`) request will succeed, if `bytes _data` is populated, then this corresponds to a check on `transferWithData` (or `transferFromWithData`) will succeed.

``` solidity
function canTransfer(address _from, address _to, uint256 _amount, bytes _data) external view returns (byte, bytes32);
```

#### transferWithData

Transfer restrictions can take many forms and typically involve on-chain rules or whitelists. However for many types of approved transfers, maintaining an on-chain list of approved transfers can be cumbersome and expensive. An alternative is the co-signing approach, where in addition to the token holder approving a token transfer, and authorised entity provides signed data which further validates the transfer.

The `bytes _data` allows arbitrary data to be submitted alongside the transfer, for the token contract to interpret or record. This could be signed data authorising the transfer (e.g. a dynamic whitelist) but is flexible enough to accomadate other use-cases.

`transferWithData` MUST emit a `Transfer` event with details of the transfer.

``` solidity
function transferWithData(address _to, uint256 _amount, bytes _data) external;
```

#### transferFromWithData

This is the analogy to the `transferWithData` function.

`msg.sender` MUST have a sufficient `allowance` set and this `allowance` must be debited by the `_amount`.

``` solidity
function transferFromWithData(address _from, address _to, uint256 _amount, bytes _data) external;
```

### Token Issuance

#### isIssuable

A security token issuer can specify that issuance has finished for the token (i.e. no new tokens can be minted or issued).

If a token returns FALSE for `isIssuable()` then it MUST always return FALSE in the future.

If a token returns FALSE for `isIssuable()` then it MUST never allow additional tokens to be issued.

``` solidity
function isIssuable() external view returns (bool);
```

#### issue

This function must be called to increase the total supply.

The `bytes _data` parameter can be used to inject off-chain data (e.g. signed data) to authorise or authenticate the issuance and receiver of issued tokens.

When called, this function MUST emit the `Issued` event.

``` solidity
function issue(address _tokenHolder, uint256 _amount, bytes _data) external;
```

### Token Redemption

#### redeem

Allows a token holder to redeem tokens.

The redeemed tokens must be subtracted from the total supply and the balance of the token holder. The token redemption should act like sending tokens and be subject to the same conditions.

The `Redeemed` event MUST be emitted every time this function is called.

As with `transferWithData` this function has a `bytes _data` parameter that can be used in the token contract to authenticate the redemption.

``` solidity
function redeem(uint256 _amount, bytes _data) external;
```

#### redeemFrom

This is the analogy to the `redeem` function.

`msg.sender` MUST have a sufficient `allowance` set and this `allowance` must be debited by the `_amount`.

The `Redeemed` event MUST be emitted every time this function is called.

``` solidity
function redeemFrom(address _tokenHolder, uint256 _amount, bytes _data) external;
```

### Controller Operation

In some jurisdictions the issuer (or an entity delegated to by the issuer) may need to retain the ability to force transfer tokens. This could be to address a legal dispute or court order, or to remedy an investor losing access to their private keys.

#### controllerTransfer

This function allows an authorised address to transfer tokens between any two token holders. The transfer must still respect the balances of the token holders (so the transfer must be for at most `balanceOf(_from)` tokens) and potentially also need to respect other transfer restrictions.

`controllerTransfer` MUST emit a ControllerTransfer event.

``` solidity
function controllerTransfer(address _from, address _to, uint256 _value, bytes _data, bytes _operatorData) external;
```

#### controllerRedeem

This function allows an authorised address to redeem tokens for any token holder. The redemption must still respect the balances of the token holder (so the redemption must be for at most `balanceOf(_tokenHolder)` tokens) and potentially also need to respect other transfer restrictions.

`controllerTransfer` MUST emit a ControllerRedemption event.

``` solidity
function controllerRedeem(address _tokenHolder, uint256 _amount, bytes _data, bytes _operatorData) external;
```

#### isControllable

In order to provide transparency over whether `controllerTransfer` / `controllerRedeem` are useable, the function `isControllable` can be used.

If a token returns FALSE for `isControllable()` then it MUST:
  - always return FALSE in the future.
  - `controllerTransfer` must revert
  - `controllerRedeem` must revert  

In other words, if an issuer sets `isControllable` to return FALSE, then there can be no controller transactions for this token contract.

``` solidity
function isControllable() external view returns (bool);
```

## Interface

``` solidity
/// @title IERC1594 Security Token Standard
/// @dev See https://github.com/SecurityTokenStandard/EIP-Spec

interface IERC1594 is IERC20 {

    // Document Management
    function getDocument(bytes32 _name) external view returns (string, bytes32);
    function setDocument(bytes32 _name, string _uri, bytes32 _documentHash) external;

    // Transfers
    function transferWithData(address _to, uint256 _value, bytes _data) external;
    function transferFromWithData(address _from, address _to, uint256 _value, bytes _data) external;

    // Controller Operation
    function isControllable() external view returns (bool);
    function controllerTransfer(address _from, address _to, uint256 _value, bytes _data, bytes _operatorData) external;
    function controllerRedeem(address _tokenHolder, uint256 _amount, bytes _data, bytes _operatorData) external;

    // Token Issuance
    function isIssuable() external view returns (bool);
    function issue(address _tokenHolder, uint256 _amount, bytes _data) external;

    // Token Redemption
    function redeem(uint256 _amount, bytes _data) external;
    function redeemFrom(address _tokenHolder, uint256 _amount, bytes _data) external;

    // Transfer Validity
    function canTransfer(address _from, address _to, uint256 _amount, bytes _data) external view returns (byte, bytes32);

    // Events
    event ControllerTransfer(
        address controller,
        address indexed from,
        address indexed to,
        uint256 amount,
        bytes data,
        bytes operatorData
    );

    event ControllerRedemption(
        address controller,
        address indexed tokenHolder,
        uint256 amount,
        bytes data,
        bytes operatorData
    );

    event Issued(address indexed operator, address indexed to, uint256 amount, bytes data);

    event Redeemed(address indexed operator, address indexed from, uint256 amount, bytes data);

    event Document(bytes32 indexed name, string uri, bytes32 documentHash);

}
```

## References
- [EIP 1400: Security Token Standard With Partitions](https://github.com/ethereum/EIPs/issues/1411)
- [EIP 1410: Partially Fungible Token Standard](https://github.com/ethereum/EIPs/issues/1410)
- [EIP Draft](https://github.com/SecurityTokenStandard/EIP-Spec)
