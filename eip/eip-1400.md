---

eip: ERC-1400
title: Security Token Standard With Tranches
author: Adam Dossa (@adamdossa), Pablo Ruiz (@pabloruiz55), Fabian Vogelsteller (@frozeman), Stephane Gosselin (@thegostep)
discussions-to: #1411
status: Draft
type: Standards Track
category: ERC
created: 2018-09-09
require: ERC-1410 (#1410), ERC-1594 (#1594), ERC-20 (#20), ERC-1066 (#1066)

---

## Simple Summary

Acts as an umbrella standard to combine other standards which are relevant and useful for issuing and managing security tokens.

In aggregate provides a standard interface for issuing security tokens, managing their ownership and transfer restrictions and providing transparency to token holders on how different subsets of their token balance behave with respect to transfer restrictions, rights and obligations. Combines ERC-1410 (#1410) and ERC-1594 (#1594).

## Abstract

Builds on the partially fungible token standard (ERC-1410 (#1410)) and the security token standard (ERC-1594 (#1594)) to define functionality to manage different types of ownership of fungible tokens representing asset ownership.

The standard is backwards compatible with ERC-20 (#20) and can be easily extended to be compatible with ERC-777 (#777).

## Motivation

Accelerate the issuance and management of securities on the Ethereum blockchain by specifying a standard interface through which security tokens can be operated on and interrogated by all relevant parties.

The security token standard (#1594) provides document management, error signalling, gate keeper (operator) access control, off-chain data injection and issuance / redemption semantics.

The partially fungible token standard (#1411) provides an interface to manage and expose partially fungible subsets of a token holders balance.

Security tokens will generally include some level of transfer restrictions which may apply differently, to different subsets of a token holders balance. There may also be different rights and obligations associated with these subsets. As an example lock-up periods may vary depending on when and from whom tokens were acquired and a token holder may own tokens that fall into several categories.

The security token standard provides an interface to manage these restrictions whilst the partially fungible token standard provides an interface for investors to gain some transparency into their partitioned holdings and the metadata associated with these partitions.

## Requirements

Moving the issuance, trading and lifecycle events of a security onto a public ledger requires having a standard way of modelling securities, their ownership and their properties on-chain.

The following requirements have been compiled following discussions with parties across the Security Token ecosystem.

- MUST have a standard interface to query if a transfer would be successful and return a reason for failure.
- MUST be able to perform forced transfer for legal action or fund recovery.
- MUST emit standard events for issuance and redemption.
- MUST be able to attach metadata to a subset of a token holder's balance such as special shareholder rights or data for transfer restrictions.
- MUST be able to modify metadata at time of transfer based on off-chain data, on-chain data and the parameters of the transfer.
- MAY require signed data to be passed into a transfer transaction in order to validate it on-chain.
- SHOULD NOT restrict the range of asset classes across jurisdictions which can be represented.
- MUST be ERC-20 compatible.
- COULD be ERC-777 compatible.

## Rationale

There are many types of securities which, although they represent the same underlying asset, need to have differentiating data tied to them.

This additional metadata implicitly renders these securities non-fungible, but in practice this data is usually applied to a subset of the security rather than an individual security. The ability to partition a token holder's balance into tranches, each with separate metadata is addressed in the Partially Fungible Token section.

For example a token holder's balance may be split in two: those tokens issued during the primary issuance, and those received through secondary trading.

Security token contracts can reference this metadata in order to apply additional logic to determine whether or not a transfer is valid, and determine the metadata that should be associated with the tokens once transferred into the receiver's balance.

To represent securities metadata we use ERC-1410 (#1410) - Partially Fungible Token Standard.

Transfers of securities can fail for a variety of reasons in contrast to utility tokens which generally only require the sender to have a sufficient balance.

These conditions could be related to metadata of the securities being transferred (i.e. whether they are subject to a lock-up period), the identity of the sender and receiver of the securities (i.e. whether they have been through a KYC process, whether they are accredited or an affiliate of the issuer) or for reasons unrelated to the specific transfer but instead set at the token level (i.e. the token contract enforces a maximum number of investors or a cap on the percentage held by any single investor).

For ERC-20 tokens, the `balanceOf` and `allowance` functions provide a way to check that a transfer is likely to succeed before executing the transfer, which can be executed both on and off-chain.

For tokens representing securities the standard introduces a function `canTransfer` / `canTransferByTranche` which provides a more general purpose way to achieve this when the reasons for failure are more complex; and a function of the whole transfer (i.e. includes any data sent with the transfer and the receiver of the securities).

In order to provide a richer result than just true or false, a byte return code is returned. This allows us to give a reason for why the transfer failed, or at least which category of reason the failure was in. The ability to query documents and the expected success of a transfer is included in Security Token section.

In order to support off-chain data inputs to transfer functions, transfer functions are extended to `transferWithData` / `transferFromWithData` which can optionally take an additional `bytes _data` parameter.

To represent transfer restrictions we use ERC-1594 (#1594) - Security Token Standard.

## Specification

This standard does not specify any additional functions, but combines ERC-1410 (#1410) and ERC-1594 (#1594) to provide both standard security token functionality as well as tranches (partially fungible tokens) to provide further transparency on transfer restrictions.

In order to combine these two standards, the additional constraints are specified.

### operatorTransferByTranche

If the token is controllable (`isControllable` returns `TRUE`) then the controller may use `operatorTransferByTranche` without being explicitly authorised by the token holder.

In this instance, the `operatorTransferByTranche` MUST also emit a ControllerTransfer event.

Correspondingly, if `isControllable` returns `FALSE` then the controller cannot call `operatorTransferByTranche` unless explicitly authorised by the token holder.

### operatorRedeemByTranche

If the token is controllable (`isControllable` returns `TRUE`) then the controller may use `operatorRedeemByTranche` without being explicitly authorised by the token holder.

In this instance, the `operatorRedeemByTranche` MUST also emit a ControllerRedemption event.

Correspondingly, if `isControllable` returns `FALSE` then the controller cannot call `operatorRedeemByTranche` unless explicitly authorised by the token holder.

### Default Tranches

In order for `transfer` and `transferWithData` to operate on partially fungible tokens, there needs to be some notion of default tranches that these functions apply to. The details for how these are determined (e.g. either a fixed list, dynamically, or using `tranchesOf`) is left as an implementation detail rather than defined as part of the standard.

When transferring tokens as part of a `transfer` or `transferWithData` operation, these transfers should respect the invariant of partially fungible tokens, namely that the sum of the balances across all tranches should equal to the total balance of a token holder.

## Interface

``` solidity
/// @title IERC1400 Security Token Standard
/// @dev See https://github.com/SecurityTokenStandard/EIP-Spec

interface IERC1400 is IERC20 {

  // Document Management
  function getDocument(bytes32 _name) external view returns (string, bytes32);
  function setDocument(bytes32 _name, string _uri, bytes32 _documentHash) external;

  // Token Information
  function balanceOfByTranche(bytes32 _tranche, address _tokenHolder) external view returns (uint256);
  function tranchesOf(address _tokenHolder) external view returns (bytes32[]);

  // Transfers
  function transferWithData(address _to, uint256 _value, bytes _data) external;
  function transferFromWithData(address _from, address _to, uint256 _value, bytes _data) external;

  // Tranche Token Transfers
  function transferByTranche(bytes32 _tranche, address _to, uint256 _amount, bytes _data) external returns (bytes32);
  function operatorTransferByTranche(bytes32 _tranche, address _from, address _to, uint256 _amount, bytes _data, bytes _operatorData) external returns (bytes32);

  // Controller Operation
  function isControllable() external view returns (bool);
  function controllerTransfer(address _from, address _to, uint256 _value, bytes _data, bytes _operatorData) external;
  function controllerRedeem(address _tokenHolder, uint256 _amount, bytes _data, bytes _operatorData) external;

  // Operator Management
  function authorizeOperator(address _operator) external;
  function revokeOperator(address _operator) external;
  function authorizeOperatorByTranche(bytes32 _tranche, address _operator) external;
  function revokeOperatorByTranche(bytes32 _tranche, address _operator) external;

  // Operator Information
  function isOperator(address _operator, address _tokenHolder) external view returns (bool);
  function isOperatorForTranche(bytes32 _tranche, address _operator, address _tokenHolder) external view returns (bool);

  // Token Issuance
  function isIssuable() external view returns (bool);
  function issue(address _tokenHolder, uint256 _amount, bytes _data) external;
  function issueByTranche(bytes32 _tranche, address _tokenHolder, uint256 _amount, bytes _data) external;

  // Token Redemption
  function redeem(uint256 _amount, bytes _data) external;
  function redeemFrom(address _tokenHolder, uint256 _amount, bytes _data) external;
  function redeemByTranche(bytes32 _tranche, uint256 _amount, bytes _data) external;
  function operatorRedeemByTranche(bytes32 _tranche, address _tokenHolder, uint256 _amount, bytes _operatorData) external;

  // Transfer Validity
  function canTransfer(address _from, address _to, uint256 _amount, bytes _data) external view returns (byte, bytes32);
  function canTransferByTranche(address _from, address _to, bytes32 _tranche, uint256 _amount, bytes _data) external view returns (byte, bytes32, bytes32);    

  // Controller Events
  event ControllerTransfer(
      address controller,
      address indexed from,
      address indexed to,
      uint256 amount,
      bytes data,
      bytes operatorData
  );

  event ControllerRedemption(
      address controller,
      address indexed tokenHolder,
      uint256 amount,
      bytes data,
      bytes operatorData
  );

  // Document Events
  event Document(bytes32 indexed name, string uri, bytes32 documentHash);

  // Transfer Events
  event TransferByTranche(
      bytes32 indexed fromTranche,
      address operator,
      address indexed from,
      address indexed to,
      uint256 amount,
      bytes data,
      bytes operatorData
  );

  event ChangedTranche(
      bytes32 indexed fromTranche,
      bytes32 indexed toTranche,
      uint256 amount
  );

  // Operator Events
  event AuthorizedOperator(address indexed operator, address indexed tokenHolder);
  event RevokedOperator(address indexed operator, address indexed tokenHolder);
  event AuthorizedOperatorByTranche(bytes32 indexed tranche, address indexed operator, address indexed tokenHolder);
  event RevokedOperatorByTranche(bytes32 indexed tranche, address indexed operator, address indexed tokenHolder);

  // Issuance / Redemption Events
  event Issued(address indexed operator, address indexed to, uint256 amount, bytes data);
  event Redeemed(address indexed operator, address indexed from, uint256 amount, bytes data);
  event IssuedByTranche(bytes32 indexed tranche, address indexed operator, address indexed to, uint256 amount, bytes data, bytes operatorData);
  event RedeemedByTranche(bytes32 indexed tranche, address indexed operator, address indexed from, uint256 amount, bytes operatorData);


}
```

### Notes

#### Restricted Transfers

Transfers of security tokens can fail for a number of reasons in contrast to utility tokens which generally only require the sender to have a sufficient balance.

These conditions could be related to metadata of the security tokens being transferred (i.e. whether they are subject to a lock-up period), the identity and eligibility of the sender and receiver of the tokens (i.e. whether they have been through a KYC process and whether they are accredited or an affiliate of the issuer) or for reasons unrelated to the specific transfer but instead set at the security level for regulatory purposes (i.e. the security enforces a maximum number of investors or a cap on the percentage held by any single investor).

For utility tokens (ERC-20 / ERC-777) the `balanceOf` and `allowance` functions provide a way to check that a transfer is likely to succeed before executing the transfer which, can be executed both on and off-chain.

The standard includes a function `canTransfer` which provides a more general purpose way to query if sending tokens would be successful. It accepts a set of parameters which may include signed data and returns a reason byte code with information about the success or failure of the transaction.

NB - the result of a call to `canTransfer` may change depending on on-chain state (including block numbers or timestamps) and possibly off-chain oracles. As such, it does not provide guarantees that a future transfer will be successful after being called as a view function that does not modify any state.

#### Identity

Under many jurisdictions, whether a party is able to receive and send security tokens depends on the characteristics of the party's identity. For example, most jurisdictions require some level of KYC / AML process before a party is eligible to purchase or sell a particular security. Additionally, a party may be categorized into an investor qualification category (e.g. accredited investor, qualified purchaser), and their citizenship may also inform restrictions associated with their securities.

There are various identity standards (e.g. ERC-725 (#725), Civic, uPort) which can be used to capture the party's identity data, as well as other approaches which are centrally managed (e.g. maintaining a whitelist of addresses that have been approved from a KYC perspective). These identity standards have in common to key off an Ethereum address (which could be a party's wallet, or an identity contract), and as such the `canTransfer` function can use the address of both the sender and receiver of the security token as a proxy for identity in deciding if eligibility requirements are met.

Beyond this, the standard does not mandate any particular approach to identity.

#### Reason Codes

To improve the token holder experience, `canTransfer` MUST return a reason byte code on success or failure based on the EIP-1066 application-specific status codes specified below. An implementation can also return arbitrary data as a `bytes32` to provide additional information not captured by the reason code.

| Code   | Reason                                                        |
| ------ | ------------------------------------------------------------- |
| `0x50` | 	transfer failure                                             |
| `0x51` | 	transfer success                                             |
| `0x52` | 	insufficient balance                                         |
| `0x53` | 	insufficient allowance                                       |
| `0x54` | 	transfers halted (contract paused)                           |
| `0x55` | 	funds locked (lockup period)                                 |
| `0x56` | 	invalid sender                                               |
| `0x57` | 	invalid receiver                                             |
| `0x58` | 	invalid operator (transfer agent)                            |
| `0x59` |                                                               |
| `0x5a` |                                                               |
| `0x5b` |                                                               |
| `0x5a` |                                                               |
| `0x5b` |                                                               |
| `0x5c` |                                                               |
| `0x5d` |                                                               |
| `0x5e` |                                                               |
| `0x5f` | 		token meta or info

These codes are being discussed at:  
https://ethereum-magicians.org/t/erc-1066-ethereum-status-codes-esc/283/24

#### On-chain vs. Off-chain Transfer Restrictions

The rules determining if a security token can be sent may be self-executing (e.g. a rule which limits the maximum number of investors in the security) or require off-chain inputs (e.g. an explicit broker approval for the trade). To facilitate the latter, the `transferByTranche`, `transferWithData`, `transferFromWithData`, `canTransferByTranche` and `canTransfer` functions accept an additional `bytes _data` parameter which can be signed by an approved party and used to validate a transfer.

The specification for this data is outside the scope of this standard and would be implementation specific.

## References
- [EIP 1410: Partially Fungible Token Standard](https://github.com/ethereum/EIPs/issues/1410)
- [EIP 1594: Security Token Standard](https://github.com/ethereum/EIPs/issues/1594)
- [EIP Draft](https://github.com/SecurityTokenStandard/EIP-Spec)

_Copied from original issue_: https://github.com/ethereum/EIPs/issues/1400
