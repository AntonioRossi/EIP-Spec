---

eip: ERC1410
title: Partially Fungible Token Standard
author: Adam Dossa (@adamdossa), Pablo Ruiz (@pabloruiz55), Fabian Vogelsteller (@frozeman), Stephane Gosselin (@thegostep)
discussions-to: #1410
status: Draft
type: Standards Track
category: ERC
created: 2018-09-13
require: ERC-1066 (#1066)

---

## Simple Summary

A standard interface for organising an owners tokens into a set of tranches.

## Abstract

Describes an interface to support an owners tokens being grouped into tranches, with each tranche being represented by an identifying key and a balance.

Tokens are operated upon at a tranche granularity, but data about the overall supply of tokens and overall balances of owners is also tracked.

This standard can be combined with ERC20 or ERC777 to provide an additional layer of granular transparency as to the behaviour of a token contract on different partitions of a token holders balance.

## Motivation

Being able to associate metadata with individual fungible tokens is useful when building functionality associated with those tokens.

For example, knowing when an individual token was minted allows vesting or lockup logic to be implemented for a portion of a token holders balance.

Tokens that represent securities often require metadata to be attached to individual tokens, such as restrictions associated with the share.

Being able to associate arbitrary metadata with groups of tokens held by users is useful in a variety of use-cases. It can be used for token provenance (i.e. recording the previous owner(s) of tokens) or to attach data to a token which is then used to determine any transfer restrictions of that token.

In general it may be that whilst tokens are fungible under some circumstances, they are not under others (for example in-game credits and deposited balances). Being able to define such groupings and operate on them whilst maintaining data about the overall distribution of a token irrespective of this is useful in modelling these types of assets.

Having a standard way to identify groupings of tokens within an overall balance helps provides token holders transparency over their balances.

## Rationale

A Partially-Fungible Token allows for attaching metadata to a partial balance of a token holder. These partial balances are called tranches and are indexed by a `bytes32 _tranche` key which can be associated with metadata on-chain or off-chain.

The specification for this metadata, beyond the existence of the `_tranche` key to identify it, does not form part of this standard. The token holders address can be paired with the tranche to use as a metadata key if data varies across token holders with the same tranche (e.g. a "restricted" tranche may be associated with different lock up dates for each token holder).

For an individual owner, each token in a tranche therefore shares common metadata.

Token fungibility includes metadata so we have:
  - for a specific user, tokens within a given tranche are fungible
  - for a specific user, tokens from different tranches may not be fungible

Note - tranches with the same `bytes32` key across different users may be associated with different metadata depending on the implementation.

## Backwards Compatibility

This standard is un-opinionated on ERC20 vs. ERC777. It can be easily combined with either standard, and we expect this to usually be the case. We don't define the standard token view functions (`name`, `symbol`, `decimals`) as a consequence.

In order to remain backwards compatible with ERC20 / ERC777 (and other fungible token standards) it is necessary to define what tranche or tranches are used when a `transfer` / `send` operation is executed (i.e. when not explicitly specifying the tranche). However this is seen as an implementation detail (could be via a fixed list, or programatically determined). One option is to simple iterate over all `tranchesOf` for the token holder, although this approach needs to be cognisant of block gas limits.

## Specification

### Token Information

#### balanceOf

Aggregates a token holders balances across all tranches. Equivalent to `balanceOf` in the ERC20/777 specification.

MUST count the sum of all tranche balances assigned to a token holder.

``` solidity
function balanceOf(address _tokenHolder) external view returns (uint256);
```

#### balanceOfByTranche

As well as querying total balances across all tranches through `balanceOf` there may be a need to determine the balance of a specific tranche.

For a given token holder, the sum of `balanceOfByTranche` across `tranchesOf` MUST be equal to `balanceOf`.

``` solidity
function balanceOfByTranche(bytes32 _tranche, address _tokenHolder) external view returns (uint256);
```

#### tranchesOf

A token holder may have their balance split into several partitions (tranches) - this function will return all of the tranches associated with a particular token holder address.

``` solidity
function tranchesOf(address _tokenHolder) external view returns (bytes32[]);
```

#### totalSupply

Returns the total amount of tokens issued across all token holders and tranches.

MUST count all tokens tracked by this contract.

``` solidity
function totalSupply() external view returns (uint256);
```

### Tokens Transfers

Token transfers always have an associated source and destination tranche, as well as the usual amounts and sender / receiver addresses.

As an example, a permissioned token may use tranche metadata to enforce transfer restrictions based on:
  - the `_tranche` value
  - any additional data associated with the `_tranche` value (e.g. a lockup timestamp that may be associated with `_tranche`)
  - any details associated with the sender or receiver of tokens (e.g. has their identity been established)
  - the amount of tokens being transferred (e.g. does it respect any daily or other period-based volume restrictions)
  - the `_data` parameter allows the caller to supply any additional authorisation or details associated with the transfer (e.g. signed data from an authorised entity who is permissioned to authorise the transfer)

Other use-cases include tracking provenance of tokens by associating previous holders with destination tranches.

#### transferByTranche

This function MUST throw if the transfer of tokens is not successful for any reason.

When transferring tokens from a particular tranche, it is useful to know on-chain (i.e. not just via an event being fired) the destination tranche of those tokens. The destination tranche will be determined by the implementation of this function and will vary depending on use-case.

The function MUST return the `bytes32 _tranche` of the receiver.

The `bytes _data` allows arbitrary data to be submitted alongside the transfer, for the token contract to interpret or record. This could be signed data authorising the transfer (e.g. a dynamic whitelist), or provide some input for the token contract to determine the receivers tranche.

This function MUST emit a `TransferByTranche` event for successful transfers.

This function MUST emit a `ChangedTranche` event if the tranche of the receiver differs to the sender.

``` solidity
function transferByTranche(bytes32 _tranche, address _to, uint256 _amount, bytes _data) external returns (bytes32);
```

#### operatorTransferByTranche

Allows an operator to transfer security tokens on behalf of a token holder, within a specified tranche.

This function MUST revert if called by an address lacking the appropriate approval as defined by `isOperatorForTranche`.

This function MUST emit a `TransferByTranche` event for successful token transfers, and include the operator address.

This function MUST emit a `ChangedTranche` event if the tranche of the receiver differs to the sender.

The return data is interpreted consistently with `transferByTranche`.

``` solidity
function operatorTransferByTranche(bytes32 _tranche, address _from, address _to, uint256 _amount, bytes _data, bytes _operatorData) external returns (bytes32);
```

#### canTransferByTranche

Transfers of partially fungible tokens may fail for a number of reasons, relating either to the token holders partial balance, or rules associated with the tranche being transferred.

The standard provides an on-chain function to determine whether a transfer will succeed, and return details indicating the reason if the transfer is not valid.

These rules can either be defined using smart contracts and on-chain data, or rely on `_data` passed as part of the `transferByTranche` function which could represent authorisation for the transfer (e.g. a signed message by a transfer agent attesting to the validity of this specific transfer).

The function will return both a ESC (Ethereum Status Code) following the EIP-1066 standard, and an additional `bytes32` parameter that can be used to define application specific reason codes with additional details (for example the transfer restriction rule responsible for making the transfer operation invalid).

It also returns the destination tranche of the tokens being transferred in an analogous way to `transferByTranche`.

``` solidity
function canTransferByTranche(address _from, address _to, bytes32 _tranche, uint256 _amount, bytes _data) external view returns (byte, bytes32, bytes32);
```

### Operators

Operators can be authorised at a token level for all token holders, or by individual token holders.

  - all token holders and all tranches (`defaultOperators`)
  - all token holders for a specific tranche (`defaultOperatorsByTranche`)
  - a specific token holder and all tranches (`authorizeOperator`, `revokeOperator`, `isOperatorFor`)
  - a specific token holder for a specific tranche (`authorizeOperatorByTranche`, `revokeOperatorByTranche`, `isOperatorForTranche`)

A token level operator could for example be a trusted contract with functionality linked to the tokens utility. How these operators are authorised or managed is not in the scope of this standard.

If ERC-20 or ERC-777 is extended, the behaviour of `allowances` / `defaultOperators` in those standards should be mapped to tranche behaviour.

#### defaultOperators

This function returns the set of default operators who are authorised for all token holders and all tranches.

MUST query a list of operators which can operate over all addresses and tranches

``` solidity
function defaultOperators() external view returns (address[]);
```

#### defaultOperatorsByTranche

This function returns the set of default operators who are authorised for all token holders and a specified tranche.

``` solidity
function defaultOperatorsByTranche(bytes32 _tranche) external view returns (address[]);
```

#### authorizeOperator

Allows a token holder to set an operator for their tokens across all tranches.

MUST authorise an operator for all tranches of `msg.sender`

This function MUST emit the event `AuthorizedOperator` every time it is called.

``` solidity
function authorizeOperator(address _operator) external;
```

#### revokeOperator

Allows a token holder to revoke an operator for their tokens across all tranches.

NB - it is possible the operator will retain authorisation over this token holder and some tranches through `authorizeOperatorByTranche`.

MUST revoke authorisation of an operator previously given for all tranches of `msg.sender`

This function MUST emit the event `RevokedOperator` every time it is called.

``` solidity
function revokeOperator(address _operator) external;
```

#### isOperatorFor

Returns whether a specified address is an operator for the given token holder and all tranches.

This should return TRUE if the address is an operator under any of the above categories.

MUST query whether `_operator` is an operator for all tranches of `_tokenHolder`.

``` solidity
function isOperatorFor(address _operator, address _tokenHolder) external view returns (bool);
```

#### authorizeOperatorByTranche

Allows a token holder to set an operator for their tokens on a specific tranche.

This function MUST emit the event `AuthorizedOperatorByTranche` every time it is called.

``` solidity
function authorizeOperatorByTranche(bytes32 _tranche, address _operator) external;
```

#### revokeOperatorByTranche

Allows a token holder to revoke an operator for their tokens on a specific tranche.

NB - it is possible the operator will retain authorisation over this token holder and tranche through either `defaultOperatorsByTranche` or `defaultOperators`.

This function MUST emit the event `RevokedOperatorByTranche` every time it is called.

``` solidity
function revokeOperatorByTranche(bytes32 _tranche, address _operator) external;
```

#### isOperatorForTranche

Returns whether a specified address is an operator for the given token holder and tranche.

This should return TRUE if the address is an operator under any of the above categories.

``` solidity
function isOperatorForTranche(bytes32 _tranche, address _operator, address _tokenHolder) external view returns (bool);
```

### Interface

``` solidity
/// @title ERC-1410 Partially Fungible Token Standard
/// @dev See https://github.com/SecurityTokenStandard/EIP-Spec

interface IERC1410 {

    // Token Information
    function balanceOf(address _tokenHolder) external view returns (uint256);
    function balanceOfByTranche(bytes32 _tranche, address _tokenHolder) external view returns (uint256);
    function tranchesOf(address _tokenHolder) external view returns (bytes32[]);
    function totalSupply() external view returns (uint256);

    // Token Transfers
    function transferByTranche(bytes32 _tranche, address _to, uint256 _amount, bytes _data) external returns (bytes32);
    function operatorTransferByTranche(bytes32 _tranche, address _from, address _to, uint256 _amount, bytes _data, bytes _operatorData) external returns (bytes32);
    function canTransferByTranche(address _from, address _to, bytes32 _tranche, uint256 _amount, bytes _data) external view returns (byte, bytes32, bytes32);    

    // Operator Information

    TODO: delete default stuff

    function defaultOperators() external view returns (address[]);
    function isOperator(address _operator, address _tokenHolder) external view returns (bool);
    function defaultOperatorsByTranche(bytes32 _tranche) external view returns (address[]);
    function isOperatorForTranche(bytes32 _tranche, address _operator, address _tokenHolder) external view returns (bool);

    // Operator Management
    function authorizeOperator(address _operator) external;
    function revokeOperator(address _operator) external;
    function authorizeOperatorByTranche(bytes32 _tranche, address _operator) external;
    function revokeOperatorByTranche(bytes32 _tranche, address _operator) external;

    // Issuance / Redemption
    function issueByTranche(bytes32 _tranche, address _tokenHolder, uint256 _amount, bytes _data) external;
    function redeemByTranche(bytes32 _tranche, uint256 _amount, bytes _data) external;
    function operatorRedeemByTranche(bytes32 _tranche, address _tokenHolder, uint256 _amount, bytes _operatorData) external;

    // Transfer Events
    event TransferByTranche(
        bytes32 indexed fromTranche,
        address operator,
        address indexed from,
        address indexed to,
        uint256 amount,
        bytes data,
        bytes operatorData
    );

    event ChangedTranche(
        bytes32 indexed fromTranche,
        bytes32 indexed toTranche,
        uint256 amount,
    );

    // Operator Events
    event AuthorizedOperator(address indexed operator, address indexed tokenHolder);
    event RevokedOperator(address indexed operator, address indexed tokenHolder);
    event AuthorizedOperatorByTranche(bytes32 indexed tranche, address indexed operator, address indexed tokenHolder);
    event RevokedOperatorByTranche(bytes32 indexed tranche, address indexed operator, address indexed tokenHolder);

    // Issuance / Redemption Events
    event IssuedByTranche(bytes32 indexed tranche, address indexed operator, address indexed to, uint256 amount, bytes data, bytes operatorData);
    event RedeemedByTranche(bytes32 indexed tranche, address indexed operator, address indexed from, uint256 amount, bytes operatorData);

}
```
